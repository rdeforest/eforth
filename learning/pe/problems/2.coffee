###

Even Fibonacci numbers

Each new term in the Fibonacci sequence is generated by adding the previous
two terms. By starting with 1 and 2, the first 10 terms will be:

    1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not exceed
four million, find the sum of the even-valued terms.

###

{fibonacci} = require '../sequences'

module.exports =
  (upTo = 4 * 10 ** 6, filter = ((n) -> not (n & 1)), sequence = fibonacci) ->
    seq = sequence()
    n = seq.next().value
    done = false
    total = 0

    while n <= upTo
      if filter n
        total += n

      {value, done} = seq.next()

      if done
        return total

      n = value

    return total

